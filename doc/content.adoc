= struct - Structural validation library
:toc: left
:!numbered:
:idseparator: -
:idprefix:
:sectlinks:
:source-highlighter: pygments
:pygments-style: friendly


== Introduction

A structural validation library for Clojure and ClojureScript.

Is highly based on the ideas from
link:https://github.com/leonardoborges/bouncer[bouncer]
but with simplified implementantion, builtin coersion support and no macros usage
(validators are simple dicts).


=== Project Maturity

Since _struct_ is a young project there may be some API breakage.


=== Install

Just include that in your depencency vector on *_project.clj_*:

[source,clojure]
----
[funcool/struct "0.1.0"]
----


== User guide

=== Quick Start

Let's require the main _struct_ namespace:

[source, clojure]
----
(require '[struct.core :as st])
----

Define a small schema for the example purpose:

[source, clojure]
----
(def +scheme+
  {:name [st/required st/string]
   :year [st/required st/number]})
----

You can observe that it consists in a simple map when you declare keys and
corresponding validators for that key. A vector as value allows us to put
more than one validator for the same key. If you have only one validator for the
key, you can omit the vector and put it as single value.

By default, all validators are optional so if the value is missing, no error
will reported. If you want make the value mandatory, you should use a specific
`required` validator.

And finally, start validating your data:

[source, clojure]
----
(-> {:name "Blood of Elves" :year 1994}
    (st/validate +scheme+))
;; => [nil {:name "Blood of Elves" :year 1994}]

(-> {:name "Blood of Elves" :year "1994"}
    (st/validate +scheme+))
;; => [{:year ("must be a number")} {:name "Blood of Elves", :year "1994"}]

(-> {:year "1994"}
    (st/validate +scheme+))
;; => [{:name ("this field is mandatory"), :year ("must be a number")} {}]
----

If only want to know if some data is valid or not, you can use the `valid?` predicate
for that purpose:

[source, clojure]
----
(st/valid? {:year "1994"} +scheme+)
;; => false
----

The additional entries in the map will be striped by default, but this
can be turned off passing an additional flag as third argument:

[source, clojure]
----
(-> {:name "Blood of Elves" :year 1994 :foo "bar"}
    (st/validate +scheme+))
;; => [nil {:name "Blood of Elves" :year 1994}]

(-> {:name "Blood of Elves" :year 1994 :foo "bar"}
    (st/validate +scheme+ {:strip false}))
;; => [nil {:name "Blood of Elves" :year 1994 :foo "bar"}]
----


=== Parametrized validators

Additionally to simple validators, other may require additional parameters
(e.g. `in-range`). This is how them can be passed to the validator:

[source, clojure]
----
(def schema {:num [[st/in-range 10 20]]})

(st/validate {:num 21} schema)
;; => [{:num ("not in range")} {}]

(st/validate {:num 19} schema)
;; => [nil {:num 19}]
----

Note the double vector, the outer denotes a list of validatiors and the inner
denotes a validator with patameters.


=== Custom messages

The builtin validators comes with default messages in human readable format, but
sometimes you may want to change them (e.g. for i18n purposes). This is how you
can do it:

[source, clojure]
----
(def schema
  {:num [[st/in-range 10 20 :message "errors.not-in-range"]]})

(st/validate {:num 21} schema)
;; => [{:num ("errors.not-in-range")} {}]
----

=== Data coersions

Additionally to simple validation process, this library includes the facility
for coerce values and a collection of validators that matches over strings. Let
see some code:

.Example attaching custom coersions
[source, clojure]
----
(def schema
  {:year [[st/integer :coerce str]]})

(st/validate {:year 1994} schema))
;; => [nil {:year "1994"}]
----

From previous example, you can see that the data returned from the validation
process, the value is properly coerced with the specified coersion function.

Furthermore, this library comes with a collection of validators that already
has attached coersion function, and them serves just for validate paraeters
that comes in string format and convert later to the appropriate type:

[source, clojure]
----
(def schema {:year [st/required st/integer-like]
             :id [st/required st/uuid-like]})

(st/validate {:year "1994"
              :id "543e7472-6624-4cb5-b65e-f3c341843d0f"}
             schema)
;; => [nil {:year 1994, :id #uuid "543e7472-6624-4cb5-b65e-f3c341843d0f"}]
----

For facilitate this operation, it there `validate!` function that receives the
data and scheme and returns the resulting data. If data not matches the schema
an exception will be raised using `ex-info` clojure facility:

[source, clojure]
----
(st/validate! {:year "1994" :id "543e7472-6624-4cb5-b65e-f3c341843d0f"} schema)
;; => {:year 1994, :id #uuid "543e7472-6624-4cb5-b65e-f3c341843d0f"}
----

=== Builtin Validators

This is the table with available builtin validators:

.Builtin Validators
[options="header", cols="2,1,4"]
|===========================================================================
| Identifier                | Coersion | Description
| `struct.core/keyword`     | no       | Validator for clojure's keyword
| `struct.core/uuid`        | no       | Validator for UUID's
| `struct.core/uuid-str`    | yes      | Validator for uuid strings with coersion to UUID
| `struct.core/email`       | no       | Validator for email string.
| `struct.core/required`    | no       | Marks field as required.
| `struct.core/number`      | no       | Validator for Number.
| `struct.core/number-str`  | yes      | Validator for number string.
| `struct.core/integer`     | no       | Validator for integer.
| `struct.core/integer-str` | yes      | Validator for integer string.
| `struct.core/boolean`     | no       | Validator for boolean.
| `struct.core/boolean-str` | yes      | Validator for boolean string.
| `struct.core/string`      | no       | Validator for string.
| `struct.core/string-like` | yes      | Validator for string like.
| `struct.core/in-range`    | no       | Validator for a number range.
| `struct.core/member`      | no       | Validator for check if a value is member of coll.
| `struct.core/positive`    | no       | Validator for positive number.
| `struct.core/negative`    | no       | Validator for negative number.
| `struct.core/function`    | no       | Validator for IFn interface.
| `struct.core/vector`      | no       | Validator for clojure vector.
| `struct.core/map`         | no       | Validator for clojure map.
| `struct.core/set`         | no       | Validator for clojure set.
| `struct.core/coll`        | no       | Validator for clojure coll.
| `struct.core/every`       | no       | Validator for check if pred match for every item in coll.
|===========================================================================

Additional notes:

* `number-str` coerces to `java.lang.Double` or `float` (cljs)
* `boolean-str` coerces to `true` (`"t"`, `"true"`, `"1"`) or `false` (`"f"`, `"false"`, `"0"`).
* `string-like` coerces anything to string using `str` function.


== Developers Guide

=== Contributing

Unlike Clojure and other Clojure contrib libs, does not have many restrictions for
contributions. Just open a issue or pull request.


=== Get the Code

_struct_ is open source and can be found on
link:https://github.com/funcool/struct[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/struct
----


=== Run tests

To run the tests execute the following:

For the JVM platform:

[source, text]
----
lein test
----

And for JS platform:

[source, text]
----
./scripts/build
node out/tests.js
----

You will need to have nodejs installed on your system.


=== License

_struct_ is under public domain:

----
This is free and unencumbered software released into the public domain.

Anyone is free to copy, modify, publish, use, compile, sell, or
distribute this software, either in source code form or as a compiled
binary, for any purpose, commercial or non-commercial, and by any
means.

In jurisdictions that recognize copyright laws, the author or authors
of this software dedicate any and all copyright interest in the
software to the public domain. We make this dedication for the benefit
of the public at large and to the detriment of our heirs and
successors. We intend this dedication to be an overt act of
relinquishment in perpetuity of all present and future rights to this
software under copyright law.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.

For more information, please refer to <http://unlicense.org/>
----
